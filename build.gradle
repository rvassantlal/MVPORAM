plugins {
	id("java")
	id('distribution')
	id 'org.hidetake.ssh' version '2.10.1'
}

group 'MVPORAM'
version '1.0'

compileJava {
	sourceCompatibility = 8
	targetCompatibility = 8
}

repositories {
	mavenCentral()
}

jar {
	archivesBaseName='MVPORAM'
	project.version=""
}

ssh.settings {
	fileTransfer = 'scp'
}

remotes {
	master {
		//Create a gradle.properties and set the following properties: remoteDeployMasterHost, remoteDeployMasterUser,
		// remoteDeployMasterPrivateKeyFile
		if (project.hasProperty("remoteDeployMasterHost")) {
			host = remoteDeployMasterHost
		}
		if (project.hasProperty("remoteDeployMasterUser")) {
			user = remoteDeployMasterUser
		}
		if (project.hasProperty("remoteDeployMasterPrivateKeyFile")) {
			identity = file(remoteDeployMasterPrivateKeyFile)
		}
	}
}

distributions {
	main {
		contents {
			into('config') {
				from 'config'
			}
			into('lib') {
				from jar
				from(configurations.runtimeClasspath)
			}
			from 'runscripts/smartrun.sh'
			from 'runscripts/smartrun.cmd'
		}
	}
}

task localDeploy(dependsOn: installDist) {
	doLast {
		int nWorkers = project.hasProperty('nWorkers') ? project.nWorkers.toInteger() : 6

		def fileSeparator = System.getProperty("file.separator")
		def src = project.buildDir.name + fileSeparator + "install" + fileSeparator + project.name
		def workingDirectory = project.buildDir.name + fileSeparator + "local" + fileSeparator

		println ("Deploying project into ${workingDirectory}")
		for (i in 0..<nWorkers) {
			def target = workingDirectory + "worker${i}"
			copy {
				from src
				into target
			}
		}

		def target = workingDirectory + "controller"
		copy {
			from src
			into target
		}
	}
}

task remoteDeploy(dependsOn: installDist) {
	doLast {
		def fileSeparator = System.getProperty("file.separator")
		def src = project.buildDir.name + fileSeparator + "install" + fileSeparator + project.name
		println(src)
		def target = '~/'
		ssh.run {
			session(remotes.master) {
				put from: src, into: target
			}
		}
	}
}

clean.doLast {
	def fileSeparator = System.getProperty("file.separator")
	def logsDir = file("logs")
	println(logsDir)
	if (logsDir.exists()) {
		logsDir.deleteDir()
	}
}

dependencies {
	implementation fileTree('lib'){include '*.jar'}

	// https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk15on
	implementation 'org.bouncycastle:bcpkix-jdk15on:1.69'

	// https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
	implementation 'org.bouncycastle:bcprov-jdk15on:1.69'

	// https://mvnrepository.com/artifact/org.bouncycastle/bcutil-jdk15on
	implementation 'org.bouncycastle:bcutil-jdk15on:1.69'

	// https://mvnrepository.com/artifact/commons-codec/commons-codec
	implementation 'commons-codec:commons-codec:1.15'

	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation 'org.apache.commons:commons-lang3:3.12.0'

	// https://mvnrepository.com/artifact/ch.qos.logback/logback-core
	implementation 'ch.qos.logback:logback-core:1.2.5'

	// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
	implementation 'ch.qos.logback:logback-classic:1.2.5'

	// https://mvnrepository.com/artifact/io.netty/netty-all
	implementation 'io.netty:netty-all:4.1.67.Final'

	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation 'org.slf4j:slf4j-api:1.7.32'

	// https://mvnrepository.com/artifact/com.google.code.gson/gson
	implementation 'com.google.code.gson:gson:2.8.8'

	// https://mvnrepository.com/artifact/org.apache.commons/commons-math3
	implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}
